apply plugin: 'com.android.application'

def baseAppId = "com.demos.buildtypes"
def appVersionName = "1.0"
def appVersionCode = 1

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    project.archivesBaseName = "BuildTypes-${appVersionName}-${appVersionCode}"

    defaultConfig {
        applicationId baseAppId
        minSdkVersion 15
        targetSdkVersion 23
        versionCode appVersionCode
        versionName appVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "inapp", "version"

    productFlavors {
        premium {
            dimension "version"
            manifestPlaceholders = [ appLabel: "BuildTypes Premium"]
        }

        free {
            dimension "version"
            manifestPlaceholders = [ appLabel: "BuildTypes Free"]
        }

        google {
            dimension "inapp"
        }

        samsung {
            dimension "inapp"
        }

        amazon {
            dimension "inapp"
        }
    }

    applicationVariants.all { variant ->
        def mergedFlavor = variant.mergedFlavor
        def flavors = variant.productFlavors
        def appId = baseAppId
        for (int i = 0; i < flavors.size(); i++) {
            appId += "."
            appId += flavors[i].name
        }
        println "flavor: ${variant.flavorName} app id: ${appId}"
        mergedFlavor.setApplicationId(appId);
    }
}

dependencies {
    compile fileTree(dir: 'libs/common', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'

    amazonCompile fileTree(dir: 'libs/amazon', include: ['*.jar'])
    samsungCompile project(':iap3Helper')
}
